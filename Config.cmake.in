@PACKAGE_INIT@

set(NOWIDE_STANDALONE @NOWIDE_STANDALONE@)
set(NOWIDE_USE_FILESYSTEM @NOWIDE_USE_FILESYSTEM@)

if(NOT NOWIDE_STANDALONE)
  # Default boost libs are static on windows and dynamic on linux
  if(WIN32 AND "${Boost_USE_STATIC_LIBS}" STREQUAL "")
    set(Boost_USE_STATIC_LIBS ON)
  endif()
  include(CMakeFindDependencyMacro)
  if(NOWIDE_USE_FILESYSTEM)
    find_dependency(Boost 1.55 COMPONENTS system)
  else()
    find_dependency(Boost 1.55)
  endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")

if(NOT WIN32)
  if(NOT TARGET nowide::shared)
	add_library(nowide::shared ALIAS nowide::nowide)
  endif()
  if(NOT TARGET nowide::static)
	add_library(nowide::static ALIAS nowide::nowide)
  endif()
elseif(NOT TARGET nowide::nowide)
  if(TARGET nowide::static)
	add_library(nowide::nowide ALIAS nowide::static)
  elseif(TARGET nowide-shared)
    add_library(nowide::nowide ALIAS nowide::shared)
  endif()
endif()

check_required_components("@PROJECT_NAME@")
